--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _kD9CUIxPEei2UKUDmonytQ,_74j9YLaIEeiiZL0aNB5Nyg,_lGnj8Ix1Eei2UKUDmonytQ,_4GRncI0bEei2UKUDmonytQ
Element-Name: test-generate-report-dialog
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _rI7VYASsEeupd5XMAdmhgA
Runtime-Version: 2.5.1.M3
Save-Time: 3/18/22, 3:26 PM
Tags: bp
Testcase-Type: ecl
Verifications: _wm5P0CtBEeuMltfUUtdvug

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

bp-import-vna "Informationsverbund-1.vna"
wait-for-import "Modernisierter IT-Grundschutz" "importierte Objekte/Informationsverbund-1 \\[b71753\\]"

get-menu -path "Datei/Report erzeugen..." | click

proc "cannot-select" [val combo] [val value] {
	verify-error {
		$combo | select $value
	}
}

with [get-window Report] {
	let [val scopeCompo [get-group "" |get-combo -after [get-label "Geltungsbereich:"]]]
	        [val reportCompo [get-group "" | get-combo -after [get-label "Report auswählen:"]]]
	        [val ouputFormatCompo [get-group "" | get-combo -after [get-label "Ausgabeformat"]]]
            [val resetButton [get-group "" | get-button "Zurücksetzen"]]{
    	// combo boxes should be empty when opening the dialog
    	$scopeCompo | get-property selection | is-empty | verify-true
    	$reportCompo | get-property selection | is-empty | verify-true
    	$ouputFormatCompo | get-property selection | is-empty | verify-true

    	// when selecting a scope, the reports should be filtered
    	$scopeCompo | select "Informationsverbund-1"
    	cannot-select $reportCompo "(S) ITGS-alt: A.1 Strukturanalyse"
    	$ouputFormatCompo | get-property selection | is-empty | verify-true

    	// when selecting a matching report, the output format should be auto-filled
    	$reportCompo | select "(S) ITGS: A.1 Strukturanalyse"
    	$ouputFormatCompo | get-property selection | equals "Portable Document Format (PDF)" | verify-true

    	// all combo boxes should be reset when clicking the button
    	$resetButton | click
    	$scopeCompo | get-property selection | is-empty | verify-true
    	$reportCompo | get-property selection | is-empty | verify-true
	    $ouputFormatCompo | get-property selection | is-empty | verify-true

	    // when selecting only a report, the output format should be auto-filled
    	$reportCompo | select "(S) ITGS: A.1 Strukturanalyse"
    	$ouputFormatCompo | get-property selection | equals "Portable Document Format (PDF)" | verify-true
    	$scopeCompo | get-property selection | is-empty | verify-true

		// when switching to a report that does not support the format, a matching one should be selected
    	$reportCompo | select "(S) ISM-ISO: Export: Aufgaben (de)"
    	$ouputFormatCompo | get-property selection | equals "Excel Format (XLSX)" | verify-true
    	$scopeCompo | get-property selection | is-empty | verify-true

		// when switching back to a report that supports all formats, the first available format is selected
    	$reportCompo | select "(S) ITGS: A.1 Strukturanalyse"
    	$ouputFormatCompo | get-property selection | equals "Portable Document Format (PDF)" | verify-true
    	$scopeCompo | get-property selection | is-empty | verify-true
		$resetButton | click

		// changing the report does not change the scope
		$scopeCompo | select "Informationsverbund-1"
	    $reportCompo | select "(S) ITGS: A.1 Strukturanalyse"
	    $scopeCompo | get-property selection | equals "Informationsverbund-1" | verify-true
	    $reportCompo | select "(S) ITGS: A.2 Schutzbedarfsfeststellung"
	    $scopeCompo | get-property selection | equals "Informationsverbund-1" | verify-true
    }
    get-button "Abbrechen" | click

}

delete-bpelement "importierte Objekte/Informationsverbund-1 \\[b71753\\]"

verify-error {get-window Fehler }

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
