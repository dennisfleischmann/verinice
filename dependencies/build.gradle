plugins {
  id "org.standardout.bnd-platform" version "1.7.0"
}

repositories {
	jcenter()
	maven {
		url 'http://conjars.org/repo/'
	}
}

platform {
	// configure proper names for the p2 update site
	categoryId = 'Verinice-Eclipse-Libs'
	categoryName = 'Verinice Eclipse Libs'

	generatePlatformFeature = false

	defaultQualifier = ''
	useBndHashQualifiers = false
	useFeatureHashQualifiers = false
	defaultQualifierMap.prefix = ''

	determineImportVersions = true
    importVersionStrategy = MINIMUM

	feature(id: 'org.hibernate', name: 'hibernate-core', version: '3.6.11') {
		plugin('org.hibernate:hibernate-core:3.6.10.Final') {
			bnd {
				optionalImport 'net.sf.cglib.beans'
				optionalImport 'net.sf.cglib.core'
				optionalImport 'net.sf.cglib.proxy'
				optionalImport 'net.sf.cglib.reflect'
				optionalImport 'net.sf.cglib.transform'
				optionalImport 'net.sf.cglib.transform.impl'

				instruction 'Eclipse-BuddyPolicy', 'registered'
			}
		}
		plugin('dom4j:dom4j:1.6.1') {
			bnd {
				optionalImport 'com.sun.msv.datatype'
				optionalImport 'com.sun.msv.datatype.xsd'
				optionalImport 'org.gjt.xpp'
				optionalImport 'org.jaxen'
				optionalImport 'org.jaxen.dom4j'
				optionalImport 'org.jaxen.pattern'
				optionalImport 'org.jaxen.saxpath'
				optionalImport 'org.relaxng.datatype'
				optionalImport 'org.xmlpull.v1'
			}
		}
	}

	feature(id: 'jfree', name: 'jfreechart-swt', version: '1.0.0') {
		plugin('jfree:jfreechart-swt:1.0.9') {
			exclude group: 'org.eclipse.swt', module: 'org.eclipse.swt.cocoa.macosx.x86_64'
		}
		plugin('jfree:jfreechart:1.0.13')
	}

	feature(id: 'pax', name: 'pax', version: '0.6.0') {
		plugin fileTree("${project.projectDir}/pax/") {
   			include '**/*.jar'
   			exclude '**/*sources.jar'
		}
	}

	feature(id: 'apache', name: 'apache', version: '0.8.0') {
		plugin fileTree("${project.projectDir}/apache/") {
   			include '**/*.jar'
   			exclude '**/*sources.jar'
   			exclude 'org.apache.servicemix.bundles.elasticsearch-with-sernet-patch-1.4.1_1'
		}//we have patchd the org.elasticsearch.monitor.jvm.JvmInfo class to be compatible with java 11
		plugin fileTree ("${project.projectDir}/apache/org.apache.servicemix.bundles.elasticsearch-with-sernet-patch-1.4.1_1.jar"), {
            bnd {
                bundleName = 'elasticsearch java 11 patches for verinice'
                version =  '1.4.1.' + new Date().format('yyyyMMddHHmmss')

                instruction 'Change', 'custom_sernet_build'
                instruction 'Build-Date', new Date().format('yyyyMMddHHmmss')
            }
        }
	}

	feature(id: 'spring', name: 'spring', version: '0.9.0') {
		plugin fileTree("${project.projectDir}/spring/") {
   			include '**/*.jar'
   			exclude '**/*sources.jar'
            exclude 'spring-web-2.5.6.SEC03'
            exclude 'spring-webmvc-2.5.6.SEC03'
            exclude 'spring-core-verinice-patch.jar'
		}
		//we have patched these two jars, we changed the dependency from javax.servlet [2.4,3.0.0) to [2.4,4.0.0) to enable javax.servlet 3.1.0
        plugin fileTree ("${project.projectDir}/spring/spring-web-2.5.6.SEC03.jar"), {
            bnd {
                version =  '2.5.6.SEC03-' + new Date().format('yyyyMMddHHmmss')
            }
        }
        plugin fileTree ("${project.projectDir}/spring/spring-webmvc-2.5.6.SEC03.jar"), {
            bnd {
                version =  '2.5.6.SEC03-' + new Date().format('yyyyMMddHHmmss')
            }
        }//we have patched this jar with the content of /sernet.springclient/src/org/springframework/core/JdkVersion.java
        plugin fileTree ("${project.projectDir}/spring/spring-core-verinice-patch.jar"), {
            bnd {
                version =  '2.5.6.SEC03-' + new Date().format('yyyyMMddHHmmss')
            }
        }

	}

    // this is part of the javav 11 patch, here we fake the existence of some packages to please the old gods of osgi
    feature(id: 'verinicepatches', name: 'verinicepatches', version: '1.0.0') {
       plugin fileTree ("${project.projectDir}/patches/verinice-rmi.jar"), {
            bnd {
                symbolicName = 'org.verinicepatches'
                bundleName = 'java 11 patches for verinice'
                version =  '1.0.0'

                instruction 'Export-Package', "javax.rmi, org.omg.CORBA, org.omg.CosNaming"
                instruction 'Change', 'custom_sernet_build'
                instruction 'Build-Date', new Date().format('yyyyMMddHHmmss')
            }
        }
    }

	feature(id: 'com.mchange.c3p0', name: 'c3p0', version: '0.9.5.4') {
		plugin('com.mchange:c3p0:0.9.5.4')
		plugin('com.mchange:mchange-commons-java:0.2.15')
		plugin('com.typesafe:config:1.3.0')
	}
}
